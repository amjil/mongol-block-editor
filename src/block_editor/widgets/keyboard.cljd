(ns block-editor.widgets.keyboard
  (:require 
   ["package:flutter/material.dart" :as m]
   [virtual-keyboard.keyboard :as keyboard]
   [virtual-keyboard.options :as options]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [virtual-keyboard.input-control :as control]
   [menu-bar.menu :as menu]
   [cljd.flutter :as f]))
   
(defn keyboard []
  (f/widget
   :context ctx
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (.-colorScheme theme)]
   :watch [visible control/visible]
   (m/Visibility
    .visible visible
    .child
    (m/TextFieldTapRegion
     .child
     (m/Container
      .color (.-surfaceVariant color-scheme)
      .child
      (keyboard/keyboard {:custom-fns {}}))))))

(defn candidates-panel []
  (f/widget
   :context ctx
   :get [:info :state]
   :let [width (-> m/MediaQuery (.of ctx) .-size .-width)]
   :watch [{candidates-list :keyboard/candidates-list} state]
   (m/Positioned
    .bottom (+ options/keyboard-default-height (* 4 (+ 4 (:keyboard/row-vertical-padding info))))
    .right (/ width 4)
    .child
    (menu/menu {:bar {:elavation 10}
                :item {:on-tap (fn [x] (keyboard-action/on-candidates-clicked x state))
                       :text-style (m/TextStyle .fontSize 18 .fontFamily "OyunQaganTig" .color m.Colors/black)}}
               (take 30 candidates-list)))))

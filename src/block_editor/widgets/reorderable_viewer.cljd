(ns block-editor.widgets.reorderable-viewer
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [block-editor.utils.block :as block]
   [block-editor.state :as state]
   [block-editor.widgets.richtext-viewer :as richtext-viewer]
   [block-editor.widgets.text-field :as text-field]
   [block-editor.widgets.header :as header]
   [block-editor.widgets.placeholder :as placeholder]
   [block-editor.widgets.todo :as todo]
   [block-editor.utils.selection :as selection]
   [block-editor.utils.color :as color-utils]))

(defn reorderable-view []
  (f/widget
   :watch [ids (f/$ (f/<! state/ids))]
   :managed [controller (m/TextEditingController. .text "")]
   (m/Padding
    .padding (m/EdgeInsets.symmetric .horizontal 8))
   (m/ReorderableListView.builder
    ;; horizontal scroll
    .scrollDirection m/Axis.horizontal
    .itemCount (count ids)
    .onReorder block/reorder-blocks
    .itemBuilder
    (f/build
     [idx]
     :key (m/ValueKey (nth @state/ids idx))
     :context ctx
     :let [block-id (nth ids idx)
           theme (-> m/Theme (.of ctx))]
    :watch [block-content (f/$ (get (f/<! state/blocks) block-id))
            is-todo (f/$ (get-in (f/<! state/block-meta) [block-id "todo"]))
            bg-color-argb (f/$ (get-in (f/<! state/block-meta) [block-id "bg-color"]))
            text-color-argb (f/$ (get-in (f/<! state/block-meta) [block-id "text-color"]))
            level (f/$ (get (f/<! state/levels) block-id 0))
            eidx (f/$ (f/<! state/edit-index))]
    :let [is-editing (= eidx idx)
          is-empty (empty? block-content)
          is-child-block (not (zero? level))
          bg-color (color-utils/argb->color bg-color-argb)
          text-color (color-utils/argb->color text-color-argb)
          divider-color (or (.-dividerColor theme)
                            (-> theme .-colorScheme .-onSurface (.withOpacity 0.12)))]
     (m/GestureDetector
      .onTap (partial selection/on-click-reorderable-item controller idx)
      .onDoubleTap (partial selection/on-click-reorderable-item controller idx))
     (m/IntrinsicWidth)
     (m/Column
      .mainAxisAlignment m.MainAxisAlignment/spaceAround
      .crossAxisAlignment m/CrossAxisAlignment.start
      .children
      [(if is-child-block
         (m/Column
          .children
          (flatten
           (map (fn [_]
                  [(m/Padding
                    .padding (m/EdgeInsets.symmetric .horizontal 4)
                    .child
                    (m/Divider
                     .color divider-color
                     .thickness 1))
                   (m/Padding
                    .padding (m/EdgeInsets.only .top 20))])
                (range level))))
         (m/SizedBox))
       (if (true? is-todo)
         (m/SizedBox)
         (header/header {:size 6 :index idx}))
       (m/Expanded
        .child
        (m/AnimatedContainer
         .duration (Duration .milliseconds 200)
         .curve m/Curves.easeInOut
         .decoration (m/BoxDecoration
                      .color (or bg-color m/Colors.transparent)
                      .borderRadius (m/BorderRadius.circular 12))
         .padding (m/EdgeInsets.all 8)
         .child
         (m/Column
          .children
          [(if (true? is-todo)
             (todo/todo {:index idx})
             (m/SizedBox))
           (m/Expanded
            .child
            (if is-editing
              (let [editor-style (m/TextStyle
                                  .fontSize (state/get-font-size)
                                  .fontFamily (state/get-font-family)
                                  .color (or text-color (-> theme .-colorScheme .-onSurface)))]
                (text-field/text-field
                 {:controller controller
                  :style editor-style
                  :on-changed (partial selection/on-update controller idx)
                  :on-tap (fn []
                            (selection/cursor-in-double-brackets controller))}))
              (if is-empty
                (placeholder/placeholder)
                (richtext-viewer/richtext-viewer
                 block-content
                 {:on-link-tap (fn [link-data]
                                 (let [target-id (:id link-data)
                                       target-title (:title link-data)]
                                   (when target-id
                                     ;; If id exists, it's a note id, call note-tap-handler
                                     (when-let [handler @state/note-tap-handler]
                                       (dart:core/print "in block-item ---")
                                       (handler {"id" target-id "title" target-title})))))
                  :on-tag-tap (fn [tag-data]
                                (let [tag-name (:tag tag-data)]
                                  (when tag-name
                                    ;; Call tag-tap-handler
                                    (when-let [handler @state/tag-tap-handler]
                                      (handler {"tag" tag-name})))))
                  :text-color text-color}))))])))])))))


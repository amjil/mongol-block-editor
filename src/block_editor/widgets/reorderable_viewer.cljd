(ns block-editor.widgets.reorderable-viewer
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [block-editor.widgets.richtext-viewer :as richtext-viewer]
   [block-editor.widgets.text-field :as text-field]
   [block-editor.widgets.header :as header]
   [block-editor.widgets.placeholder :as placeholder]
   [block-editor.utils.selection :as selection]
   [block-editor.utils.block :as block]
   [block-editor.state :as state]))

(defn reorderable-view []
  (f/widget
   :watch [eidx state/edit-index
           ids state/ids
           blocks state/blocks]
   :managed [controller (m/TextEditingController. .text "")]
   (m/ReorderableListView.builder
    ;; horizontal scroll
    .scrollDirection m/Axis.horizontal
    .itemCount (count ids)
    .onReorder block/reorder-blocks
    .itemBuilder
    (fn [ctx idx]
      (let [block-id (nth ids idx)
            block (get blocks block-id)
            key (m/ValueKey block-id)]
        (m/GestureDetector
         .key key
         .onTap (partial selection/on-click-reorderable-item controller idx)
         .onDoubleTap (partial selection/on-click-reorderable-item controller idx)
         .child
         (m/Column
          .mainAxisAlignment m.MainAxisAlignment/spaceAround
          .crossAxisAlignment m.CrossAxisAlignment/center
          .children
          [(header/header {:size 10})
           (m/Expanded
            .child
            (m/Padding
             .key key
             .padding (m/EdgeInsets.symmetric .horizontal 8 .vertical 16)
             .child
             (if (= eidx idx)
               (text-field/text-field {:controller controller
                                       :on-changed (partial selection/on-update controller idx)
                                       :on-tap (fn []
                                                 (selection/cursor-in-double-brackets controller))})
               (if (empty? block)
                 (placeholder/placeholder)
                 (richtext-viewer/richtext-viewer block {:on-link-tap (fn [link-data]
                                                                        (let [target-id (:id link-data)
                                                                              target-title (:title link-data)]
                                                                          (when target-id
                                                                            ;; If id exists, it's a note id, call note-tap-handler
                                                                            (when-let [handler @state/note-tap-handler]
                                                                              (dart:core/print "in reorderable-viewer ---")
                                                                              (handler {"id" target-id "title" target-title})))))})))))])))))))


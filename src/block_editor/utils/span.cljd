(ns block-editor.utils.span
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/gestures.dart" :as g]
   [clojure.string :as str]
   [block-editor.state :as state]))

(defn- all-matches [text pattern type-fn]
  (let [matches (.allMatches (RegExp. pattern) text)]
    (mapv (fn [m]
            (let [full (.group m 0)
                  start (.-start m)
                  end (.-end m)
                  groups (map #(try (.group m %) (catch Exception _ nil)) (range 1 (inc (.-groupCount m))))
                  first-group (first groups)
                  second-group (second groups)
                  match (merge {:type (type-fn)
                                :content full
                                :start start
                                :end end}
                               (when first-group
                                 {:title first-group})
                               (when second-group
                                 {:id second-group}))]
              match))
          matches)))

(defn bold-italic-matches [text]
  (let [pattern "(\\*\\*\\*|___)(.+?)\\1|(\\*\\*|__)(.+?)\\3|(\\*|_)(.+?)\\5"
        matches (.allMatches (RegExp. pattern) text)]
    (mapv (fn [m]
            (let [full (.group m 0)
                  start (.-start m)
                  end (.-end m)]
              {:type (cond
                       (or (.group m 1) (.group m 2))
                       :bold-italic

                       (or (.group m 3) (.group m 4))
                       :bold

                       (or (.group m 5) (.group m 6))
                       :italic

                       :else :text)
               :content (str/replace full #"\*|_" "")
               :start start
               :end end}))
          matches)))

(defn parse-block-spans [text]
  (let [;; Support both [[title]] and [[title#id]] formats (backward compatible)
        ;; If #id is matched, prefer id from text; otherwise lookup from mapping table
        link-pattern "\\[\\[([^#\\]]+)(?:#([^\\]]+))?\\]\\]"
        tag-pattern "(?<=^|[^\\w\\]])#([\\p{IsLetter}\\w\\-]+)"
        ;; Precisely get all matches and their start/end
        link-matches (all-matches text link-pattern (constantly :link))
        tag-matches (all-matches text tag-pattern (constantly :tag))
        b-i-matches (bold-italic-matches text)
        matches (sort-by :start (concat link-matches tag-matches b-i-matches))]
    (loop [spans []
           last-end 0
           ms matches]
      (if (empty? ms)
        (if (< last-end (count text))
          (conj spans {:type :text :content (subs text last-end)})
          spans)
        (let [{:keys [start end] :as item} (first ms)
              spans (if (> start last-end)
                      (conj spans {:type :text :content (subs text last-end start)})
                      spans)
              spans (conj spans (dissoc item :start :end))]
          (recur spans end (rest ms)))))))

  (defn ^m/TextSpan create-text-span [span theme]
    ;;"Create a text span"
    (m/TextSpan
     .text (:content span)
     .style (m/TextStyle
             .color (-> theme .-colorScheme .-onSurface)
             .fontFamily (state/get-font-family)
             .height 1.5
             .fontSize (state/get-font-size))))

  (defn ^m/TextSpan create-link-span [span theme on-link-tap]
    ;;   "Create a link span"
    (let [recognizer (g/TapGestureRecognizer.)
          title (:title span)
          ;; Prefer id from text (backward compatible), otherwise lookup from mapping table
          id (or (:id span) (state/get-link-id title))
          display-text (or title (:content span))]
      (set! (.-onTap recognizer)
            (fn []
              (when (fn? on-link-tap)
                (on-link-tap {:title title :id id}))))
      (m/TextSpan
       .text display-text
       .style (m/TextStyle
               .color (-> theme .-colorScheme .-primary)
               .fontSize (state/get-font-size)
               .height 1.5
               .decoration m/TextDecoration.overline
               .fontFamily (state/get-font-family)
               .fontWeight m/FontWeight.w500)
       .recognizer recognizer)))

  (defn ^m/TextSpan create-tag-span [tag theme on-tag-tap]
    ;;   "Create a tag span"
    (let [recognizer (g/TapGestureRecognizer.)
          tag-name (str/replace tag #"^#" "")]
      (set! (.-onTap recognizer)
            (fn []
              (when (fn? on-tag-tap)
                (on-tag-tap {:tag tag-name}))))
      (m/TextSpan
       .text tag
       .style (m/TextStyle
               .fontSize (state/get-font-size)
               .height 1.5
               .fontFamily (state/get-font-family)
               .fontWeight m/FontWeight.w500
               .backgroundColor (-> theme .-colorScheme .-surface)
               .decorationStyle m/TextDecorationStyle.solid
               .decoration m/TextDecoration.overline
               .color (-> theme .-colorScheme .-primary))
       .recognizer recognizer)))

(defn create-bold-span [text theme]
  (m/TextSpan
   .text text
   .style (m/TextStyle
           .fontSize (state/get-font-size)
           .height 1.5
           .fontFamily (state/get-font-family)
           .fontWeight m/FontWeight.bold
           .color (-> theme .-colorScheme .-primary))))

(defn create-italic-span [text theme]
  (m/TextSpan
   .text text
   .style (m/TextStyle
           .fontSize (state/get-font-size)
           .height 1.5
           .fontFamily (state/get-font-family)
           .fontStyle m/FontStyle.italic
           .color (-> theme .-colorScheme .-primary))))

(defn create-bold-italic-span [text theme]
  (m/TextSpan
   .text text
   .style (m/TextStyle
           .fontSize (state/get-font-size)
           .height 1.5
           .fontFamily (state/get-font-family)
           .fontWeight m/FontWeight.bold
           .fontStyle m/FontStyle.italic
           .color (-> theme .-colorScheme .-primary))))

(defn parse-to-text-spans [text theme on-link-tap on-tag-tap]
  (let [spans (parse-block-spans text)]
    (map (fn [span]
           (cond
             (= (:type span) :link)
             (create-link-span span theme on-link-tap)

             (= (:type span) :tag)
             (create-tag-span (:content span) theme on-tag-tap)

             (= (:type span) :bold)
             (create-bold-span (:content span) theme)

             (= (:type span) :italic)
             (create-italic-span (:content span) theme)

             (= (:type span) :bold-italic)
             (create-bold-italic-span (:content span) theme)

             :else
             (create-text-span span theme)))
         spans)))
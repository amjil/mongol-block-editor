(ns block-editor.utils.selection
  (:require
   ["package:flutter/material.dart" :as m]
   [virtual-keyboard.keyboard-action :as action]
   [virtual-keyboard.input-control :as control]
   [block-editor.utils.block :as block]
   [block-editor.state :as state]))


(defn set-editing-value [controller new-text offset]
  (set! (.-value controller)
        (m/TextEditingValue.
         .text new-text
         .selection (m.TextSelection/collapsed .offset offset))))

(defn is-cursor-in-double-brackets [controller]
  (let [text (.-text controller)
        selection (.-selection controller)]
    (if (and (.-isValid selection) (.-isCollapsed selection))
      (let [cursor (.-start selection)
            open-index (.lastIndexOf text "[[" cursor)
            close-index (.indexOf text "]]" cursor)
            inside (and (not= -1 open-index)
                        (not= -1 close-index)
                        (< open-index cursor)
                        (<= cursor close-index))]
        [inside open-index close-index])
      [false -1 -1])))

(defn next-block []
  (block/create-block)
  (let [idx (inc @state/edit-index)
        block-id (get @state/ids idx)
        new-content (get @state/blocks block-id)]
    (action/clear-candidates)
    (reset! state/edit-index (inc @state/edit-index))
    (reset! state/current-id block-id)
    (control/reset-text new-content)))

(defn next-content []
  (let [editing-value @control/editing-value
        [inside _ close-index] (is-cursor-in-double-brackets editing-value)]
    (if (true? inside)
      (do
        (control/update-selection (+ 2 close-index))
        (reset! state/open-search-box false))
      (next-block))))

(defn run-search-index [text open-index close-index]
  (reset! state/open-search-box true)
  (when (< (+ 2 open-index) close-index)
    (reset! state/search-query
            (.substring text (+ 2 open-index) close-index))))
         
(defn cursor-in-double-brackets [controller]
  (let [text (.-text controller)
        [inside open-index close-index] (is-cursor-in-double-brackets controller)]
    (if (true? inside)
      (run-search-index text open-index close-index)
      (reset! state/open-search-box false))))

(defn on-click-reorderable-item [controller idx]
  (let [block-id (nth @state/ids idx)
        new-content (get @state/blocks block-id)]
    (action/clear-candidates)
    (set-editing-value controller new-content (count new-content))
    (reset! state/edit-index idx)))

(defn on-update [controller idx text]
  (let [block-id (nth @state/ids idx)]
    (when (= idx @state/edit-index)
      (block/update-block-content block-id text)))
  (cursor-in-double-brackets controller))
(ns block-editor.utils.block
  (:require 
   ["package:uuid/uuid.dart" :as uuid]
   [block-editor.state :as s]))

(def ^uuid/Uuid uuid-instance (uuid/Uuid.))

(defn reorder-blocks [old-index new-index]
  (let [blocks (vec @s/ids)
        block-to-move (nth blocks old-index)
        ;; Step 1: Remove element from old position
        without-old (vec (concat (take old-index blocks)
                                 (drop (inc old-index) blocks)))
        ;; Step 2: Insert element at new position
        new-block-ids (vec (concat (take new-index without-old)
                                   [block-to-move]
                                   (drop new-index without-old)))]

    (when (not= -1 @s/edit-index)
      (reset! s/edit-index -1))
    (reset! s/ids new-block-ids)))

(defn sort-blocks-by-ids [ids blocks]
  (let [block-ids ids 
        indexd-map (zipmap block-ids (range))]
    (vec (sort-by (fn [block] (get indexd-map (.-blockId block))) blocks))))

(defn update-block-content [id text]
  (swap! s/blocks assoc id text))

(defn create-block []
  (let [id (.v4 uuid-instance)]
    (swap! s/ids conj id)
    (swap! s/blocks assoc id "")
    (swap! s/levels assoc id 0)))

(defn delete-block [id]
  (let [block-content (get @s/blocks id)
        link-titles (s/extract-link-titles block-content)]
    ;; Remove link ids for links in the deleted block
    (doseq [title link-titles]
      (s/remove-link-id! title))
    (swap! s/ids #(vec (remove (fn [x] (= x id)) %)))
    (swap! s/blocks dissoc id)
    (swap! s/levels dissoc id)))

(defn clean []
  (reset! s/edit-index -1)
  (reset! s/ids [])
  (reset! s/blocks {})
  (reset! s/levels {}))

(defn init []
  (create-block))
